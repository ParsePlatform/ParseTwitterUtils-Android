apply plugin: "com.android.library"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
}

dependencies {

    api "androidx.appcompat:appcompat:1.1.0"

    api "oauth.signpost:signpost-core:1.2.1.2"

    api "se.akerfeldt:okhttp-signpost:1.1.0"

    api "com.github.parse-community.Parse-SDK-Android:parse:1.22.1"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.10.19"
}

//region Code Coverage

apply plugin: "jacoco"

jacoco {
    toolVersion "0.7.1.201405082137"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       '**/gdata/**',
                       '**/oauth/**',
                       '**/signpost/**']
    )

    sourceDirectories = files("${buildDir.parent}/src/main/java")
    additionalSourceDirs = files([
            "${buildDir}/generated/source/buildConfig/debug",
            "${buildDir}/generated/source/r/debug"
    ])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

//endregion

//region Coveralls

apply plugin: "com.github.kt3k.coveralls"

coveralls.jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"

//endregion

apply from: "https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/1.1.0/gradle-android-javadocs.gradle"
